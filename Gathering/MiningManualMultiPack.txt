/////////////////////////////////////////////
//    Sir Sodomizer's Manual Miner V 1.2   //
//  *** Multiple Pack Animals Edition ***  //
//   !!!ONLY IRON ORES IN PACK ANIMALS!!!  //
// Macro mines & moves iron to pack animal //
//   Notifies and pauses during captchas   //
//  Grab pickaxes, pack animals, and play  //
/////////////////////////////////////////////
// Debug
@clearjournal
@removetimer Mining
@removelist NearBy
@createlist NearBy
// Checks to see if a list of pack animals exist
if not listexists 'packanimals'
  // If it doesn't find them it creates new set of pets
  @createlist 'packanimals'
  @clearlist 'packanimals'
  // Loop 5 times, or until escape gets hit
  for 5
    headmsg "Select pack animal, or ESC to stop" 38
    promptalias 'packanimal'
    // If you selected a pet it gets added to the list
    if @findobject 'packanimal'
      pushlist 'packanimals' 'packanimal'
    else
      // If you hit escape it bails this for loop
      break
    endif
  endfor
endif
// If you mine with the same pack animals all the time add them here
//@pushlist 'packanimals'
//@pushlist 'packanimals'
//@pushlist 'packanimals'
//@pushlist 'packanimals'
//@pushlist 'packanimals'
// Opens backpack if no pickaxes found
if not @findtype 0xe86 any backpack
  useobject backpack
  pause 650
endif
// If you have more than 34 tracking
if skill "Tracking" > 34
  // Checks to see if you've used the skill recently
  if not @timerexists Tracking or @timer Tracking > 9200
    // Use the skill
    useskill 'Tracking'
    waitforgump 0xfe5c638b 15000
    replygump 0xfe5c638b 8
    waitforgump 0xfe5c638b 15000
    while not @injournal "hunt all hostile"  system
      @clearjournal
      replygump 0xfe5c638b 7
      waitforgump 0xfe5c638b 15000
      pause 250
    endwhile
    // If it says begin hunting in tracking menu
    if @ingump 0xfe5c638b "Begin Hunting"
      // Hit the button
      replygump 0xfe5c638b 6
      waitforgump 0xfe5c638b 5000
    endif
    // Close tracking menu
    replygump 0xfe5c638b 0
    @createtimer Tracking
  endif
endif
while not dead
  // Keeps working as long as world save isn't happening
  while not @injournal "world is saving" system
    // If Mining was not started, or it took longer than 2 seconds
    if not timerexists Mining or timer Mining > 2000
      @clearjournal
      // Use pickaxe, or further step in
      if not @useobject righthand
        // Check for pickaxes in pack
        if not @findtype 0xe86 any backpack
          headmsg "No pickaxes!" 38
          playsound 1086
          stop
        else
          // Put it in your hands and uses it
          equipitem found 2
          pause 1000
          @useobject righthand
        endif
      endif
      waitfortarget 2000
      // while Macrocheck then makes a sound and waits 5s
      while @ingump any "Captcha"
        playsound 1095
        pause 5000
      endwhile
      // Checks to see if you are digging
      if not @injournal "dig" system
        @clearjournal
        clearhands both
        @removetimer Mining
        pause 1000
      endif
      @createtimer Mining
      target! self
      pause 500
    endif
    // Scans system messages
    if @injournal "world is saving" system
      break
      // If ore was mined it resets
    elseif @injournal ore system
      @clearjournal
      removetimer Mining
      // If no vein around it tries again 2 more times
    elseif @injournal nearby system
      @clearjournal
      pushlist NearBy 1
      settimer Mining 500
    endif
    // Damage / Enemy alert, then pause 10s
    @getenemy 'any'
    if murderer enemy or criminal enemy
      playsound 1069
      headmsg "PK, RUN!" 38
      pause 10000
    endif
    if @injournal "Now Tracking" system
      playsound 1069
      headmsg "PK, RUN!" 38
      pause 10000
    endif
    if diffhits > 0
      playsound 1088
      headmsg "You're under attack" 38
      pause 10000
    endif
    // If it didn't find a vein 3x it alerts you
    if list NearBy > 1
      headmsg "Move to another spot" 38
      settimer Mining 1000
      @clearlist NearBy
      @clearjournal
    endif
    // If 128 stones to overloaded and 10 iron ore in pack
    if diffweight < 129 and counttype 0x19b9 0 backpack > 9
      for 0 to packanimals
        if @inrange packanimals[] 2 and @counttype 0x19b9 0 packanimals[] < 241
          // Move iron ore to pack animal
          @movetype 0x19b9 'backpack' packanimals[] 0 0 0 0 10
          pause 1000
          break
        endif
      endfor
    endif
    // If 4 stones to overloaded stop
    if diffweight < 5
      headmsg "Overweight" 38
      stop
    endif
  endwhile
  // Handles saves
  headmsg "Pausing For World Save" 38
  createtimer saving
  pause 500
  while not @injournal "save complete" system and timer saving < 20000
  endwhile
  removetimer saving
  headmsg "Save Over" 38
  @clearjournal
endwhile
headmsg "You are dead, sorry" 38

