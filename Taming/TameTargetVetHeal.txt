///////////////////////////////
// Sir Sodomizer's Tamer 1.5 //
// Vets TankPet while taming //
// If above 39% healing will //
// bandage you when injured  //
//  Hit play and Target mob  //
//  Macro stops when tamed   //
///////////////////////////////
headmsg 'Select beast to tame'
promptalias 'TameMe'
removetimer "Taming"
if not @findobject 'TameMe'
  headmsg "No creature targetted, stopping" 38
  stop
endif
////
if not distance 'TankPet' < 18
  headmsg "Target Tank Pet" 38
  promptalias 'TankPet'
endif
@clearjournal
while @inrange 'TameMe' 10
  if friend 'TameMe'
    headmsg "Tamed Successfully" 38
    @unsetalias 'TameMe'
    stop
  endif
  if not @timerexists "Taming"
    useskill 'Animal Taming'
    waitfortarget 5000
    target! 'TameMe'
    @clearjournal
    createtimer "Taming"
    warmode on
    warmode off
  endif
  if @timer "Taming" > 10000
    // Wait 3.3s to see if you've failed
    waitforjournal "fail to tame" 3300
    removetimer "Taming"
  endif
  // If you started healing
  if @timerexists "Bandage"
    // Scans system to see if you've finished
    if @injournal 'finish applying' 'system' or @timer "Bandage" > 20000
      @removetimer "Bandage"
      pause 150
      continue
    endif
    // If you haven't started healing
  else
    // If you're below 80% and have over 39 healing
    if diffhits > 20 and skill Healing > 39
      // Bandage check
      if @counttype '0xe21' 'any' 'backpack' < 20
        headmsg "Low bandages!" 38
      endif
      headmsg "Warning, you are being attacked" 38
      // Starts timer then starts bandaging
      createtimer "Bandage"
      bandage self
      @clearjournal
      // Otherwise it checks if Tank is poisoned or below 80%
    elseif diffhits 'TankPet' > 20 or poisoned 'TankPet'
      if @counttype '0xe21' 'any' 'backpack' < 20
        headmsg "Low bandages!" 38
      endif
      createtimer "Bandage"
      // Pushes timer up 5s to match up with heal times
      settimer "Bandage" 5000
      @clearjournal
      bandage 'TankPet'
    endif
  endif
  if counttype '0xe21' 'any' 'backpack' < 1
    headmsg "No bandages!" 38
    stop
  endif
endwhile
headmsg "Creature too far to tame" 38

