///////////////////////////////////
// Sir Sodomizer's MakeLast V1.2 //
// Makes last item & RECYCLE ALL //
//  Stop when Society completes  //
// Put tools & resources in pack //
// Object Inspector for Graphic  //
//  Edit macro & make item once  //
///////////////////////////////////
@createlist tool
@clearlist tool
@createlist resource
@clearlist resource
@createlist item
@clearlist item
@clearjournal
// EDIT 3 LINES BELOW
@pushlist 'tool' 0x1eb8 // Graphic of tool goes here
@pushlist 'resource' 0x1bf2 // Graphic of resource goes here
@pushlist 'item' 0xa22 // Graphic of item being recycled
// EDIT 3 LINES ABOVE, don't touch below
// Will run until you are out of tools
while counttype 'tool[0]' any backpack > 0
  // Searches for society, stops if completed
  if @injournal "completed a society job" system
    headmsg "Society job complete, stopping" 38
    @clearjournal
    stop
  endif
  // Counts resources, currently set to stop if below 16
  if @counttype 'resource[0]' any backpack < 16
    // Stops if you are below 16 resources
    headmsg "More Resources Needed" 38
    stop
  endif
  // Uses tool
  @usetype 'tool[0]' any backpack
  // Waits for crafting gump to open
  waitforgump 0x38920abd 5000
  // Checks if Recycle all is on
  while not @ingump 0x38920abd "Recycle All (of Type)"
    // Hits recycle button until recycle all shows up
    replygump 0x38920abd 2
    waitforgump 0x38920abd 5000
    pause 500
  endwhile
  // Counts if more than 5 recyclable items
  if @counttype 'item[0]' any backpack > 5
    // Searches for item to target
    @findtype 'item[0]' any backpack
    // Hits "Begin Recycling"
    replygump 0x38920abd 3
    waitfortarget 5000
    // Targets an item
    target! found
    pause 650
  else
    // Hits "Make Last Item"
    replygump 0x38920abd 7
    // Waits 1.9 seconds, edit for lag
    pause 1900
  endif
endwhile

