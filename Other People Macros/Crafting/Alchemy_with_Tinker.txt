// Alchemy script prototype
//
// Notes:
// ------
// Feel free to keep regs inside a bag
// but script uses regs in -main pack- 
// to decide which potions to make.
// 
// To start:
// =========
// 1) Put tinker tools in pack
// 2) Put 1 bottle in main pack, and only 1
//

// ****************************
// ** MODIFY THIS FOR POTION **
// ****************************
// Add exact name of potion you want to make to list
//
@removelist 'potion'
@createlist 'potion'
@pushlist 'potion' 'Lesser Poison'
//
// ****************
// ** END MODIFY **
// ****************

//
// Bag aliases to stock from
//
if not @findalias 'matsbag'
    sysmsg "Select bag with wood and ingots" 91
    promptalias 'matsbag'
endif
if not @findalias 'regbag'
    sysmsg "Select bag with reagents" 91
    promptalias 'regbag'
endif
if not @findalias 'kegbag'
    sysmsg "Select bag for filled kegs" 91
    promptalias 'kegbag'
endif

//
// Keg materials list
//
@removelist 'kegmats'
@createlist 'kegmats'
pushlist 'kegmats' 0x1bf2 // ingot
pushlist 'kegmats' 0x1bd7 // board

//
// Resolve which reg type we need
//
@removelist 'regs'
@createlist 'regs'
if @inlist 'potion' 'Lesser Heal'
    pushlist 'regs' 0xf85 // ginseng
elseif @inlist 'potion' 'Heal'
    pushlist 'regs' 0xf85 // ginseng
elseif @inlist 'potion' 'Greater Heal'
    pushlist 'regs' 0xf85 // ginseng
elseif @inlist 'potion' 'Lesser Cure'
    pushlist 'regs' 0xf84 // garlic
elseif @inlist 'potion' 'Cure'
    pushlist 'regs' 0xf84 // garlic
elseif @inlist 'potion' 'Greater Cure'
    pushlist 'regs' 0xf84 // garlic
elseif @inlist 'potion' 'Refresh'
    pushlist 'regs' 0xf7a // pearl
elseif @inlist 'potion' 'Total Refresh'
    pushlist 'regs' 0xf7a // pearl
elseif @inlist 'potion' 'Strength'
    pushlist 'regs' 0xf86 // root
elseif @inlist 'potion' 'Greater Strength'
    pushlist 'regs' 0xf86 // root
elseif @inlist 'potion' 'Agility'
    pushlist 'regs' 0xf7b // moss
elseif @inlist 'potion' 'Greater Agility'
    pushlist 'regs' 0xf7b // moss
elseif @inlist 'potion' 'Lesser Magic Resist'
    pushlist 'regs' 0xf8d // silk
elseif @inlist 'potion' 'Magic Resist'
    pushlist 'regs' 0xf8d // silk
elseif @inlist 'potion' 'Greater Magic Resist'
    pushlist 'regs' 0xf8d // silk
elseif @inlist 'potion' 'Lesser Poison'
    pushlist 'regs' 0xf88 // shade
elseif @inlist 'potion' 'Poison'
    pushlist 'regs' 0xf88 // shade
elseif @inlist 'potion' 'Greater Poison'
    pushlist 'regs' 0xf88 // shade
elseif @inlist 'potion' 'Deadly Poison'
    pushlist 'regs' 0xf88 // shade
elseif @inlist 'potion' 'Lethal Poison'
    pushlist 'regs' 0xf88 // shade
elseif @inlist 'potion' 'Lesser Explosion'
    pushlist 'regs' 0xf8c // ash
elseif @inlist 'potion' 'Explosion'
    pushlist 'regs' 0xf8c // ash
elseif @inlist 'potion' 'Greater Explosion'
    pushlist 'regs' 0xf8c // ash
else
    headmsg "Unable to figure out potion type you requested"
    stop
endif

//
// Potions list
// 
@removelist 'potions'
@createlist 'potions'
pushlist 'potions' 0xf0c // heal
pushlist 'potions' 0xf07 // cure
pushlist 'potions' 0xf06 // resist
pushlist 'potions' 0xf0a // poison
pushlist 'potions' 0xf08 // agility
pushlist 'potions' 0xf0b // refresh
pushlist 'potions' 0xf0a // poison
pushlist 'potions' 0xf0d // explo
pushlist 'potions' 0xf09 // strength

//
// Check no potions in pack
// 
for 0 to 'potions'
    if findtype potions[] 'any' 'backpack'
        headmsg "Please remove all potions from pack before starting" 91
        stop
    endif
endfor

@clearjournal

//
// Main loop
//
while not dead

    //
    // Check no potions in pack
    // 
    for 0 to 'potions'
        if findtype potions[] 'any' 'backpack'
            headmsg "Unexpected potion in pack" 91
            stop
        endif
    endfor

    // 
    // Count tinker tools
    // 
    if not findtype 0x1eb8 'any' 'backpack'
        headmsg "I have no tinker tools, cannot proceed"
        stop
    endif

    //
    // Check for empty bottle
    //
    if not findtype 0xf0e 0 'backpack'
        headmsg "I need an empty bottle" 91
        stop
    endif

    //
    // Check keg or make
    //
    if not findtype 0x1940 'any' 'backpack'
        headmsg "Making a keg" 91
        // stock materials
        for 0 to 'kegmats'
            movetype kegmats[] 'matsbag' 'backpack' 0 0 0 0 12
            pause 1000
        endfor
        // check materials
        for 0 to 'kegmats'
            if counttype kegmats[] 0 'backpack' < 12
                headmsg "Unable to stock enough ingots and/or boards"
                stop
            endif
        endfor
        // make keg
        findtype 0x1eb8 'any' 'backpack'
        useobject 'found'
        waitforgump 0x38920abd 5000
        replygump 0x38920abd 108
        waitforgump 0x38920abd 5000
        replygump 0x38920abd 202
        pause 2000
        replygump 0x38920abd 0
        pause 1000

        // check keg was made
        if not findtype 0x1940 'any' 'backpack'
            headmsg "Unable to make a keg" 91
            stop
        endif
    endif

    //
    // Check mortar or make
    //
    if not findtype 0xe9b 'any' 'backpack'
        headmsg "Making a mortar" 91
        // stock materials
        movetype 0x1bf2 'matsbag' 'backpack' 0 0 0 0 5
        pause 1000
        // check materials
        if counttype 0x1bf2 0 'backpack' < 5
            headmsg "Unable to stock enough ingots" 91
            stop
        endif
        // make pestle
        findtype 0x1eb8 'any' 'backpack'
        useobject 'found'
        waitforgump 0x38920abd 5000
        replygump 0x38920abd 100
        waitforgump 0x38920abd 5000
        replygump 0x38920abd 204
        pause 2000
        replygump 0x38920abd 0
        pause 1000
        // check pestle was made
        if not findtype 0xe9b 'any' 'backpack'
            headmsg "Unable to make a pestle" 91
            stop
        endif
    endif

    //
    // Check regs to make potion
    //
    for 0 to 'regs'
        // reg check
        if @counttype regs[] 'any' 'backpack' < 100
            headmsg "Restocking regs" 91
            movetype regs[] 'regbag' 'backpack' 0 0 0 'any' 100
            pause 1500
            // check restock was good
            if @counttype regs[] 'any' 'backpack' < 100
                headmsg "Unable to restock regs" 91
                stop
            endif
        endif
    endfor

    //
    // Make potion
    // 
    findtype 0xe9b 'any' 'backpack'
    useobject 'found'
    waitforgump 0x38920abd 5000
    //
    // ** Potion select **
    //
    if @inlist 'potion' 'Lesser Heal'
        replygump 0x38920abd 100
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 200
    elseif @inlist 'potion' 'Heal'
        replygump 0x38920abd 100
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 201
    elseif @inlist 'potion' 'Greater Heal'
        replygump 0x38920abd 100
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 202
    elseif @inlist 'potion' 'Lesser Cure'
        replygump 0x38920abd 101
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 200
    elseif @inlist 'potion' 'Cure'
        replygump 0x38920abd 101
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 201
    elseif @inlist 'potion' 'Greater Cure'
        replygump 0x38920abd 101
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 201
    elseif @inlist 'potion' 'Refresh'
        replygump 0x38920abd 102
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 200
    elseif @inlist 'potion' 'Total Refresh'
        replygump 0x38920abd 102
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 201
    elseif @inlist 'potion' 'Strength'
        replygump 0x38920abd 103
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 200
    elseif @inlist 'potion' 'Greater Strength'
        replygump 0x38920abd 103
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 201
    elseif @inlist 'potion' 'Agility'
        replygump 0x38920abd 104
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 200
    elseif @inlist 'potion' 'Greater Agility'
        replygump 0x38920abd 104
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 201
    elseif @inlist 'potion' 'Lesser Magic Resist'
        replygump 0x38920abd 105
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 200
    elseif @inlist 'potion' 'Magic Resist'
        replygump 0x38920abd 105
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 201
    elseif @inlist 'potion' 'Greater Magic Resist'
        replygump 0x38920abd 105
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 202
    elseif @inlist 'potion' 'Lesser Poison'
        replygump 0x38920abd 106
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 200
    elseif @inlist 'potion' 'Poison'
        replygump 0x38920abd 106
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 201
    elseif @inlist 'potion' 'Greater Poison'
        replygump 0x38920abd 106
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 202
    elseif @inlist 'potion' 'Deadly Poison'
        replygump 0x38920abd 106
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 203
    elseif @inlist 'potion' 'Lethal Poison'
        replygump 0x38920abd 106
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 204
    elseif @inlist 'potion' 'Lesser Explosion'
        replygump 0x38920abd 107
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 200
    elseif @inlist 'potion' 'Explosion'
        replygump 0x38920abd 107
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 201
    elseif @inlist 'potion' 'Greater Explosion'
        replygump 0x38920abd 107
        waitforgump 0x38920abd 5000 
        replygump 0x38920abd 202
    else
        headmsg "Unable to figure out potion type you requested" 91
        stop
    endif
    pause 2000
    replygump 0x38920abd 0
    pause 3000
    
    //
    // Check if we need to pour into keg
    //
    if not findtype 0xf0e 0 'backpack'
        headmsg "Pouring into keg" 91
        @unsetalias 'keg'
        findtype 0x1940 'any' 'backpack'
        @setalias 'keg' 'found'
        for 0 to 'potions'
        if findtype potions[] 'any' 'backpack'
            moveitem 'found' 'keg'
            pause 1500
            break
        endif
        endfor
    endif

    //
    // Check if keg full
    //
    if @injournal 'hold any more' 'system'
        headmsg "Keg full" 91
        findtype 0x1940 'any' 'backpack'
        movetype 'found' 'kegbag'
        @clearjournal
    endif

endwhile